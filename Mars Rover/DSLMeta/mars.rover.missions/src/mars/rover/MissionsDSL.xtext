grammar mars.rover.MissionsDSL with org.eclipse.xtext.common.Terminals

generate missionsDSL "http://www.rover.mars/MissionsDSL"

Robot: 
	"DefaultSpeed:" defaultSpeed = INT
	"SlowSpeed:" slowSpeed = INT
	"RotationRange:" minAngle = INT "to"  maxAngle = INT "degrees" 
	"Missions:" missions += [Mission]+
	availableMissions += Mission+
;

Mission: "Mission" name=ID "priority" priority = INT ":"
	(
		'avoid' cond += Condition+ 'otherwise' actions += Action+ 			|
		'findOnce' cond += Condition+ 'finally' actions += Action+			|
		'findSimultaneous' cond += Condition+ 'finally' actions += Action+ 
	)
;

Condition:
	sensor = Sensor 
	relation = Relation
	value = Value
;

enum Sensor:
	light = "light"				|
	proximity = "proximity"		|
	touch = "touch"
;

enum Relation:
	EQ = "EQ"	|
	LT = "LT"	|
	LE = "LE"	|
	GT = "GT"	|
	GE = "GE"
;

Value:
	color = Color	|
	int = INT		|
	bool = BOOL
;

terminal BOOL:
	'true' | 'false'
;

Action:
	action = EV3_ACTION ("for" duration = INT | "value" value = INT)?
;

enum EV3_ACTION:
	STOP = "stop"			|
	REVERSE = "reverse" 		|
	PLAY = "play" 			|
	HALT = "halt"
;

enum Color:
	BLACK = "black"		|
	BLUE = "blue" 		|
	GREEN = "green"		|
	YELLOW = "yellow"	|
	RED = "red" 			|
	WHITE = "white"		|
	BROWN = "brown"
;