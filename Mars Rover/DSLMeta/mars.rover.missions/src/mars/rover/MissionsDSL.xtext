grammar mars.rover.MissionsDSL with org.eclipse.xtext.common.Terminals

generate missionsDSL "http://www.rover.mars/MissionsDSL"

Robot: 
	"SlaveAddress:" slaveAddress = MAC
	("SensorRefreshRate:" refreshRate = RefreshRate)? 
	"DefaultSpeed:" defaultSpeed = INT
	"SlowSpeed:" slowSpeed = INT
	"RotationRange:" minAngle = INT "to"  maxAngle = INT "degrees" 
	"StartMissions:" startMissions += [Mission]+
	availableMissions += Mission+
;

Mission: "Mission" name=ID "priority" priority = INT
	(
		type = MissionType cond += Condition+ 'ifHappens' actionsAfterSetOfConditions += Action* (newMission = NewMissions)?			|
		type = MissionType cond += Condition+ 'whenDone' actionsAfterSetOfConditions += Action* (newMission = NewMissions)?			
	)
;

RefreshRate:
	value = INT "ms"
;

terminal MAC:
	('a'..'f'|'A'..'F'|'0'..'9')('a'..'f'|'A'..'F'|'0'..'9')(':')
	('a'..'f'|'A'..'F'|'0'..'9')('a'..'f'|'A'..'F'|'0'..'9')(':')
	('a'..'f'|'A'..'F'|'0'..'9')('a'..'f'|'A'..'F'|'0'..'9')(':')
	('a'..'f'|'A'..'F'|'0'..'9')('a'..'f'|'A'..'F'|'0'..'9')(':')
	('a'..'f'|'A'..'F'|'0'..'9')('a'..'f'|'A'..'F'|'0'..'9')(':')
	('a'..'f'|'A'..'F'|'0'..'9')('a'..'f'|'A'..'F'|'0'..'9')
;

enum MissionType:
	AVOID = 						"avoid"		|
	FIND = 							"find"	|
	FINDINORDER = 			"findInOrder"	|
	FINDSIMULTANEOUS = "findSimultaneous"	
;

Condition:
	sensor = Sensor 
	(relation = Relation)?
	value = (Value)?
	('ifConditionTrue' ifConditionTrue = Action)?
;

enum Sensor:
	color = "color"				|
	proximity = "proximity"		|
	touch = "touch"
;

enum Relation:
	EQ = "EQ"	|
	LT = "LT"	|
	LE = "LE"	|
	GT = "GT"	|
	GE = "GE"
;

Value:
	color = Color	|
	integer = INT	|
	bool = BOOL
;

terminal BOOL:
	'true' | 'false'
;

Action:
	action = EV3_ACTION (value = INT)? ("for" duration = INT)?	
;

enum EV3_ACTION:
	STOP = "stop"		|
	REVERSE = "reverse" 	|
	PLAY = "play" 		|
	ROTATE = "rotate"	|
	HALT = "halt"
	// TODO: LED Colors
;

NewMissions:
	"startMission" missions +=[Mission]+
;

enum Color:
	BLACK = "black"		|
	BLUE = "blue" 		|
	GREEN = "green"		|
	YELLOW = "yellow"	|
	RED = "red" 			|
	WHITE = "white"		|
	BROWN = "brown"
;