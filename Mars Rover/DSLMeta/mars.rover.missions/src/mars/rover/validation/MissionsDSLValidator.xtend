/*
 * generated by Xtext 2.12.0
 */
package mars.rover.validation

import org.eclipse.xtext.validation.Check
import mars.rover.missionsDSL.Robot
import mars.rover.missionsDSL.MissionsDSLPackage.Literals
import mars.rover.missionsDSL.Mission
import mars.rover.missionsDSL.Action

/**
 * This class contains custom validation rules. 
 *
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#validation
 */
class MissionsDSLValidator extends AbstractMissionsDSLValidator {
	
	@Check
	def checkSpeeds(Robot robot){
		if (robot !== null){
			if (robot.defaultSpeed < 0 || robot.defaultSpeed > 100){
				error("Speeds values must be between 0 and 100", Literals.ROBOT__DEFAULT_SPEED);
			}
			if (robot.slowSpeed < 0 || robot.slowSpeed > 100){
				error("Speeds values must be between 0 and 100", Literals.ROBOT__SLOW_SPEED);
			}
		}
	}
	
	@Check
	def checkAngles(Robot robot){
		if (robot !== null){
			if (robot.minAngle < 0 || robot.minAngle > 360){
				error("Angle values must be between 0 and 360", Literals.ROBOT__MIN_ANGLE);
			}
			if (robot.maxAngle < 0 || robot.maxAngle > 360){
				error("Angle values must be between 0 and 360", Literals.ROBOT__MAX_ANGLE);
			}
		}
	}
	
	@Check
	def checkRefreshRate(Robot robot){
		if (robot !== null){
			if (robot.refreshRate !== null)
			{
				if (robot.refreshRate.value < 0){
					error("Refresh values must be positive", Literals.ROBOT__REFRESH_RATE);
				} else if (robot.refreshRate.value < 50 ){
					warning("Refresh values less than 50 might impact performance", Literals.ROBOT__REFRESH_RATE);
				}
				if (robot.refreshRate.value > 1000 ){
					warning("Refresh values greater than 1000 might lead to delays", Literals.ROBOT__REFRESH_RATE);
				}
			}
		}
	}
	
	@Check
	def checkPriorities(Mission mission){
		if (mission !== null){
			if (mission.priority < 0 || mission.priority > 100){
				error("Priority must be between 0 and 100", Literals.MISSION__PRIORITY);
			}
		}
	}
	
	@Check
	def checkDuration(Action action){
		if (action !== null && action.duration !== null){
			if (action.duration.value < 0){
				error("Duration must be positive", Literals.ACTION__DURATION);
			}
		}
	}
	
	@Check
	def checkAction(Action action){
		if (action !== null){
			switch action.action{
				case HALT: {
					if (action.value !== null){
						error("Halt doesn't support values", Literals.ACTION__VALUE);
					}
					if (action.duration !== null){
						error("Halt doesn't support duration", Literals.ACTION__DURATION);
					}
				}
				case PLAY: {
					if (action.value === null){
						error("Play needs a value", Literals.ACTION__VALUE);
					}
					if (action.duration === null){
						error("Play needs a duration in milliseconds", Literals.ACTION__DURATION);
					}
				}
				case REVERSE: {
					if (action.value !== null){
						error("Reverse doesn't support values", Literals.ACTION__VALUE);
					}
				}
				case ROTATE: {
					if (action.value !== null){
						error("Rotate doesn't support values", Literals.ACTION__VALUE);
					}
					if (action.duration !== null){
						error("Rotate doesn't support duration", Literals.ACTION__DURATION);
					}
				}
				case STOP: {
					if (action.value !== null){
						error("Stop doesn't support values", Literals.ACTION__VALUE);
					}
					if (action.duration !== null){
						error("Stop doesn't support duration", Literals.ACTION__DURATION);
					}
				}
				
			}
		}
	}
	
}

